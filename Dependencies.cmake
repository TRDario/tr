include(FetchContent)
include(CheckIncludeFileCXX)

set(LZ4_BUILD_CLI OFF)

if(TR_BUILD_SYSGFX)
	set(BUILD_SHARED_LIBS OFF)
	set(SDL_STATIC ON)
	set(SDL_SHARED OFF)
	set(SDL_DISABLE_INSTALL ON)
	set(SDL_TEST_LIBRARY OFF)
	set(SDL_AUDIO OFF)
	set(SDL_HAPTIC OFF)
	set(SDL_JOYSTICK OFF)
	set(SDL_SENSOR OFF)
	set(SDL_CAMERA OFF)
	set(SDL_RENDER OFF)
	set(SDL_POWER OFF)
	set(SDL_HIDAPI OFF)
	set(SDL_GPU OFF)
	set(SDL_OPENGLES OFF)
	set(SDL_VULKAN OFF)
	set(SDLTTF_INSTALL OFF)
	set(SDLTTF_VENDORED ON)
	set(SDLIMAGE_INSTALL OFF)
	set(SDLIMAGE_VENDORED ON)
	set(SDLIMAGE_AVIF OFF)
	set(SDLIMAGE_GIF OFF)
	set(SDLIMAGE_JPG OFF)
	set(SDLIMAGE_JXL OFF)
	set(SDLIMAGE_LBM OFF)
	set(SDLIMAGE_PCX OFF)
	set(SDLIMAGE_PNM OFF)
	set(SDLIMAGE_SVG OFF)
	set(SDLIMAGE_TGA OFF)
	set(SDLIMAGE_TIF OFF)
	set(SDLIMAGE_WEBP OFF)
	set(SDLIMAGE_XCF OFF)
	set(SDLIMAGE_XPM OFF)
	set(SDLIMAGE_XV OFF)
	set(SDLIMAGE_PNG ON)
	set(SDLIMAGE_QOI ON)
endif()

if(TR_BUILD_AUDIO)
	set(ALSOFT_EXAMPLES OFF)
	set(ALSOFT_UTILS OFF)
	set(ALSOFT_NO_CONFIG_UTIL ON)
	set(ALSOFT_INSTALL OFF)
	set(ALSOFT_INSTALL_CONFIG OFF)
	set(ALSOFT_INSTALL_HRTF_DATA OFF)
	set(ALSOFT_INSTALL_AMBDEC_PRESETS OFF)
	set(LIBTYPE "STATIC")
endif()

# GLM 1.0.2
FetchContent_Declare(glm GIT_REPOSITORY	https://github.com/g-truc/glm.git GIT_TAG 55f22ef736ba6783e95351bc71a40a48ba305afc)
FetchContent_Declare(lz4 URL https://github.com/lz4/lz4/archive/refs/tags/v1.10.0.zip SOURCE_SUBDIR build/cmake DOWNLOAD_EXTRACT_TIMESTAMP true)
FetchContent_MakeAvailable(glm lz4)
add_library(lz4::lz4 ALIAS lz4_static)

check_include_file_cxx(format TR_HAS_STD_FORMAT)
if(TR_HAS_STD_FORMAT)
	message("[tr] Found <format>.")	
	add_compile_definitions(TR_HAS_STD_FORMAT)
else()
	message("[tr] <format> not found, falling back to libfmt...")
	FetchContent_Declare(fmt URL https://github.com/fmtlib/fmt/releases/download/11.2.0/fmt-11.2.0.zip DOWNLOAD_EXTRACT_TIMESTAMP true)
	FetchContent_MakeAvailable(fmt)
endif()

if(TR_BUILD_SYSGFX)
	FetchContent_Declare(sdl3 URL https://github.com/libsdl-org/SDL/archive/refs/tags/release-3.2.16.zip OVERRIDE_FIND_PACKAGE DOWNLOAD_EXTRACT_TIMESTAMP true)
	FetchContent_Declare(sdl3_ttf GIT_REPOSITORY https://github.com/libsdl-org/sdl_ttf.git GIT_TAG release-3.2.2 GIT_SHALLOW true)
	FetchContent_Declare(sdl3_image URL https://github.com/libsdl-org/SDL_image/archive/refs/tags/release-3.2.4.zip DOWNLOAD_EXTRACT_TIMESTAMP true)
	FetchContent_MakeAvailable(sdl3_ttf sdl3_image sdl3)
endif()

if(TR_BUILD_AUDIO)
	FetchContent_Declare(ogg URL https://github.com/xiph/ogg/archive/refs/tags/v1.3.6.zip DOWNLOAD_EXTRACT_TIMESTAMP true)
	FetchContent_MakeAvailable(ogg)
	set(OGG_INCLUDE_DIR ${ogg_SOURCE_DIR}/include)
	set(OGG_LIBRARY ${ogg_SOURCE_DIR})
	FetchContent_Declare(vorbis URL https://github.com/xiph/vorbis/archive/refs/tags/v1.3.7.zip DOWNLOAD_EXTRACT_TIMESTAMP true)
	FetchContent_Declare(openal-soft URL https://github.com/kcat/openal-soft/archive/refs/tags/1.24.3.zip DOWNLOAD_EXTRACT_TIMESTAMP true)
	FetchContent_MakeAvailable(openal-soft vorbis)
endif()